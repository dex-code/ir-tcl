dnl IR toolkit for tcl/tk
dnl (c) Index Data 1995
dnl See the file LICENSE for details.
dnl $Id: configure.in,v 1.7 1995-07-28 10:28:37 adam Exp $
AC_INIT(ir-tcl.h)
CC=${CC-cc}
AC_SUBST(CC)
AC_PROG_RANLIB
AC_PROG_INSTALL
checkBoth=0
AC_CHECK_FUNC(connect, tk_checkSocket=0, tk_checkSocket=1)
if test "$tk_checkSocket" = 1; then
    AC_CHECK_LIB(socket, main, LIBS="$LIBS -lsocket", checkBoth=1)
fi
if test "$checkBoth" = 1; then
    tk_oldLibs=$LIBS
    LIBS="$LIBS -lsocket -lnsl"
    AC_CHECK_FUNC(accept, , [LIBS=$tk_oldLibs])
fi
AC_CHECK_FUNC(gethostbyname, , AC_CHECK_LIB(nsl, main, [LIBS="$LIBS -lnsl"]))
AC_STDC_HEADERS
AC_SUBST(TCLLIB)
AC_CHECK_LIB(tcl, Tcl_Eval, tclFound=1, tclFound=0, -lm)
if test "$tclFound" = 1; then
    TCLLIB=-ltcl
else
    TCLLIB=/usr/local/lib/libtcl.a
fi
AC_SUBST(YAZDIR)
AC_SUBST(MOSI)
AC_SUBST(MOSILIB)
AC_SUBST(MOSIDIR)
AC_MSG_CHECKING(for YAZ directory)
AC_ARG_WITH(yazdir,[  --with-yazdir           Yaz directory], [YAZDIR=$withval])
if test "$YAZDIR" = "yes"; then
	YAZDIR="../yaz-1.0b"
elif test "$YAZDIR" = ""; then
        YAZDIR="../yaz"
        for i in ../yaz-1.0b ../yaz-1.0b2 ../yaz-1.0b3 ../yaz-1.0b4 ../yaz-1.0 ../yaz; do
            if test -d $i; then
                YAZDIR=$i
            fi
        done
fi
AC_MSG_RESULT($YAZDIR)
if test ! -d $YAZDIR; then
	AC_MSG_WARN(YAZ directory doesnt exist)
fi
AC_MSG_CHECKING(for MOSI)
AC_ARG_WITH(mosidir, [  --with-mosidir          MOSI directory], [MOSIDIR=$withval])
if test "$MOSIDIR" = "yes"; then
	MOSIDIR="../xtimosi/src"
elif test "$MOSIDIR" = ""; then
	MOSIDIR="../xtimosi/src"
fi
if test -r ${YAZDIR}/lib/librfc.a; then
	MOSI=1
	MOSIDIR=$MOSIDIR
	MOSILIB='$(MOSIDIR)/libmosi.a $(YAZDIR)/lib/librfc.a'
        MOSITELL=yes
else
	MOSI=0
	MOSIDIR=$MOSIDIR
        MOSILIB=''
        MOSITELL=no
fi
AC_MSG_RESULT($MOSITELL)
AC_PATH_X
not_really_there=""
if test "$no_x" = ""; then
    if test "$x_includes" = ""; then
	AC_TRY_CPP([#include <X11/XIntrinsic.h>], , not_really_there="yes")
    else
	if test ! -r $x_includes/X11/Intrinsic.h; then
	    not_really_there="yes"
	fi
    fi
fi
if test "$no_x" = "yes" -o "$not_really_there" = "yes"; then
    echo checking for X11 header files
    XINCLUDES="# no special path needed"
    AC_TRY_CPP([#include <X11/Intrinsic.h>], , XINCLUDES="nope")
    if test "$XINCLUDES" = nope; then
        dirs="/usr/unsupported/include /usr/local/include /usr/X386/include /usr/include/X11R4 /usr/X11R5/include /usr/include/X11R5 /usr/openwin/include /usr/X11/include /usr/sww/include"
        for i in $dirs ; do
	    if test -r $i/X11/Intrinsic.h; then
	        XINCLUDES=" -I$i"
	    fi
        done
    fi
else
    if test "$x_includes" != ""; then
	XINCLUDES=-I$x_includes
    else
	XINCLUDES="# no special path needed"
    fi
fi
if test "$XINCLUDES" = nope; then
  echo "Warning:  couldn't find any X11 include files."
  XINCLUDES="# no include files found"
fi
AC_SUBST(XINCLUDES)
if test "$no_x" = yes; then
    XLIBSW=nope
    if test "$XLIBSW" = nope; then
	dirs="/usr/unsupported/lib /usr/local/lib /usr/X386/lib /usr/lib/X11R4 /usr/X11R5/lib /usr/lib/X11R5 /usr/openwin/lib /usr/X11/lib /usr/sww/X11/lib"
	for i in $dirs ; do
	    if test -r $i/libX11.a; then
		XLIBSW="-L$i -lX11"
	    fi
	done
    fi
else
    if test "$x_libraries" = ""; then
	XLIBSW=-lX11
    else
	XLIBSW="-L$x_libraries -lX11"
    fi
fi
if test "$XLIBSW" = nope ; then
    AC_CHECK_LIB(Xwindow, XCreateWindow, XLIBSW=-lXwindow)
fi
if test "$XLIBSW" = nope ; then
    echo "Warning:  couldn't find the X11 library archive.  Using -lX11."
    XLIBSW=-lX11
fi
AC_SUBST(XLIBSW)
AC_SUBST(TKLIB)
AC_CHECK_LIB(tk, main, tkFound=1, tkFound=0, $TCLLIB $XLIBSW -lm)
if test "$tkFound" = 1; then
    TKLIB=-ltk
else
    TKLIB=/usr/local/lib/libtk.a
fi
AC_OUTPUT(Makefile)
